import com.github.spotbugs.snom.SpotBugsTask
import io.zucchini.build.docker.DockerPlugin
import io.zucchini.build.node.NodePlugin
import io.zucchini.build.node.YarnTask

buildscript {

    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

}

plugins {
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'com.google.osdetector' version '1.6.2'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'com.github.spotbugs' version '4.5.1' apply false
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
}

/**
 * Replace version used by a group of dependencies.
 *
 * @param details Dependency details
 * @param group Group of the dependency
 * @param version Version to set
 */
void replaceDependencyGroupVersion(DependencyResolveDetails details, String group, String version) {
    if (details.requested.group == group || details.requested.group.startsWith(group + '.')) {
        details.useVersion version
    }
}


allprojects {

    group = 'io.zucchini-ui'

    apply plugin: 'idea'
    apply plugin: DockerPlugin
    apply plugin: NodePlugin

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext.getDockerTags = {
        List<String> dockerTags = []

        // Branch slug, without special characters
        String branchName = project.grgit.branch.current().name
        String branchSlug = branchName.replace('/', '-')

        // If the Git branch is the master branch, then the Docker image will be tagged "latest"
        dockerTags << "branch-${branchSlug}"
        if (branchName == 'master') {
            dockerTags << 'latest'
        }

        return dockerTags
    }

    // Plugins to use with the java plugin

    afterEvaluate {

        if (project.pluginManager.hasPlugin('java')) {

            // Add common dependencies

            project.dependencies {

                testImplementation 'junit:junit:4.13'
                testImplementation 'org.assertj:assertj-core:3.17.2'
                testImplementation 'org.mockito:mockito-core:3.5.13'

            }

            // Maven plugin

            project.apply plugin: 'maven-publish'

            // PMD plugin

            project.apply plugin: 'pmd'

            project.pmd {
                ignoreFailures = true
            }

            tasks.withType(Pmd) {
                reports {
                    xml.enabled = false
                    html.enabled = true
                }
            }

            // Spotbugs plugin
            // Only on JDK < 11
            if (JavaVersion.current() <= JavaVersion.VERSION_11) {
                project.apply plugin: 'com.github.spotbugs'

                project.spotbugs {
                    ignoreFailures = true
                    effort = 'max'
                }

                tasks.withType(SpotBugsTask) {
                    reports {
                        xml.enabled = false
                        html.enabled = true
                    }
                }
            }

        }

        // Shared Java compiler config
        project.tasks.withType(JavaCompile) {
            options.release = 11

            options.encoding = 'UTF-8'
            options.deprecation = true
            options.compilerArgs << '-parameters'
            options.compilerArgs << '-Werror'
        }

    }

    configurations.all {

        resolutionStrategy {

            // Replace commons logging by slf4j, if present
            dependencySubstitution {
                substitute module('commons-logging:commons-logging') with module('org.slf4j:jcl-over-slf4j:1.7.26')
            }

            // Override some dependency versions
            eachDependency { DependencyResolveDetails details ->
                replaceDependencyGroupVersion(details, 'org.slf4j', '1.7.26')
            }

        }

    }

    // Remove milestones, release candidates, etc from dependency updates check
    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*-${qualifier}.*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    // Docker compose config

    dockerCompose {
        environment 'ZUCCHINI_VERSION': "${project.version}"
    }

    dockerComposeUp {
        detach = true
    }

}


project("${rootProject.name}-backend") {

    apply plugin: 'java'

    dependencies {

        annotationProcessor 'org.hibernate:hibernate-validator-annotation-processor:6.1.5.Final'

        implementation 'io.dropwizard:dropwizard-core:2.0.13'
        implementation 'ma.glasnost.orika:orika-core:1.5.4'
        implementation 'xyz.morphia.morphia:core:1.4.0'
        implementation 'xyz.morphia.morphia:logging-slf4j:1.4.0'
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'org.springframework:spring-context:5.2.9.RELEASE'
        implementation 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.4.18.v20190429'
        implementation 'org.apache.commons:commons-text:1.9'

        implementation 'org.javassist:javassist:3.27.0-GA'

        implementation 'javax.xml.bind:jaxb-api:2.3.1'
        runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:2.3.3'

        testImplementation 'org.assertj:assertj-core:3.17.2'
        testImplementation 'com.pholser:junit-quickcheck-core:0.9.5'
        testImplementation 'com.pholser:junit-quickcheck-generators:0.9.5'

    }

    task runBackend(type: JavaExec, description: 'Run backend server') {

        classpath = sourceSets.main.runtimeClasspath

        main = 'io.zucchiniui.backend.BackendApplication'

        args += ['serve', "${rootDir}/server-config.yml"]
    }

    task runJsonImporter(type: JavaExec, description: 'Execute JsonImportTest') {

        classpath = sourceSets.main.runtimeClasspath

        main = 'io.zucchiniui.backend.JsonImportTest'

        if (project.hasProperty('jsonFilePath')) {
            args = [jsonFilePath]
        }
    }
}


project("${rootProject.name}-frontend") {

    apply plugin: 'java'

    project.sourceSets.main.resources.srcDir("$project.buildDir/ui-resources")

    task webpack(type: YarnTask, description: 'Run Webpack') {
        command = 'run'
        args = ['build']

        inputs.dir('src')
        inputs.file('yarn.lock')
        inputs.file('postcss.config.js')
        inputs.file('webpack.config.js')
        inputs.file('babel.config.js')
        inputs.file('.browserslistrc')

        outputs.dir('build/dist')
    }

    task jsTest(type: YarnTask, description: 'Test JavaScript') {
        command = 'test'

        inputs.dir('src')
        inputs.file('package.json')
    }

    tasks.test.dependsOn jsTest

    task lint(type: YarnTask, description: 'Lint') {
        command = 'lint'

        inputs.dir('src')
        inputs.file('.eslintrc')
        inputs.file(rootProject.file('.eslintrc'))
        inputs.file('package.json')
    }

    tasks.check.dependsOn lint

    task copyAssets(type: Copy, dependsOn: webpack) {
        from 'build/dist'
        into "$project.buildDir/ui-resources/ui"
    }

    task copyIndexHtml(type: Copy) {
        from 'index.html'
        into "$project.buildDir/ui-resources/ui"
    }

    tasks.processResources.dependsOn(copyAssets, copyIndexHtml)

}


project("${rootProject.name}-app") {

    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.zucchiniui.app.ZucchiniUIApplication'

    dependencies {

        implementation project(":${rootProject.name}-backend")
        implementation project(":${rootProject.name}-frontend")
        implementation 'io.dropwizard:dropwizard-assets:2.0.13'

    }

    runShadow {
        args 'server', "${rootDir}/server-config.yml"
    }

   tasks.assemble.dependsOn shadowJar

    docker {
        name = rootProject.name
        tags += project.getDockerTags()
    }

    dockerBuild {
        buildArg 'ZUCCHINI_VERSION': "${project.version}"
    }

}


project("${rootProject.name}-example-features") {

    apply plugin: 'java'

    dependencies {

        implementation 'info.cukes:cucumber-java:1.2.6'
        implementation 'org.slf4j:slf4j-api:1.7.26'
        implementation 'org.assertj:assertj-core:3.17.2'
        implementation 'com.google.guava:guava:29.0-jre'

    }

    task runCucumber(type: JavaExec, description: 'Run Cucumber features') {

        classpath = sourceSets.main.runtimeClasspath

        main = 'cucumber.api.cli.Main'

        args += [
            '--glue', 'classpath:io.zucchini.examples.glues',
            '--plugin', 'pretty',
            '--plugin', "html:${project.buildDir}/cucumber/html",
            '--plugin', "json:${project.buildDir}/cucumber/report.json",
            '--tags', '~@ignored',
            'src/features',
        ]

        ignoreExitValue true

    }

    task dryRunCucumber(type: JavaExec, description: 'Dry run Cucumber features') {

        classpath = sourceSets.main.runtimeClasspath

        main = 'cucumber.api.cli.Main'

        args += [
            '--glue', 'classpath:io.zucchini.examples.glues',
            '--plugin', 'pretty',
            '--plugin', "html:${project.buildDir}/cucumber-dry/html",
            '--plugin', "json:${project.buildDir}/cucumber-dry/report.json",
            '--dry-run',
            'src/features',
        ]

        ignoreExitValue true

    }

}


project("${rootProject.name}-mongo") {

    docker {
        tags += project.getDockerTags()
    }

}


project("${rootProject.name}-e2e-tests") {

    task jsTest(type: YarnTask, description: 'Test E2E') {
        command = 'test'
    }

    task lint(type: YarnTask, description: 'Lint') {
        command = 'lint'

        inputs.dir('cypress')
        inputs.file('.eslintrc')
        inputs.file(rootProject.file('.eslintrc'))
        inputs.file('package.json')
    }

    task check

    tasks.check.dependsOn lint

}
